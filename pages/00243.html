<!DOCTYPE html>
<html lang="ja">
<head>
  <meta charset="UTF-8">
  <title>Tremolec</title>
  <link rel="stylesheet" href="../styles.css">
  <link rel="icon" href="img/favicon.png" type="image/png">
</head>
<body>
<header>
</header>
<main>
  <h1>Tremolec</h1>
  <p align="right">2019年06月02日　カテゴリー：<a href="00029.html">自作エフェクター（アナログ）</a></p>
  <hr class="title"><br>
  <a href="img/02_243_1tremolecp.jpg" target="_blank"><img alt="02_243_1tremolecp.jpg" border="0" height="270" src="img/02_243_1tremolecp.jpg" width="540"/></a><br>
東京エフェクター「第6回エフェクタービルダーズ・コンテスト」に向けて製作した、トレモロを周波数帯域別にかけるというエフェクターです。全てデジタル信号処理でやる方が楽だと思いますが、製作時は知識が足りずアナログ回路＋マイコン制御の練習として取り組みました。タップテンポ付きで、LFO周期に合わせて左側のLEDが点滅します。名前の由来は、トレモロとイコライザ（Tremolo + EQ）を合わせたものです。<br>
<br>
筐体は少し変わった形にしたかったので、タカチのHEN110312Sというケースです。上下側が放熱用の形状なので、DCジャックの穴を開けるのが大変でした。ラベルデザインはKiCadでやってみましたが、フォントを取り込むのは面倒なので標準フォントをそのまま使っています。<br>
<br>
▽回路図<br>
<a href="img/02_243_2tremolecs.png" target="_blank"><img alt="02_243_2tremolecs.png" border="0" height="280" src="img/02_243_2tremolecs.png" width="560"/></a><br>
一般的なグラフィックイコライザ回路の可変抵抗部分をフォトカプラLCR0202で置き換えたものとなります。DAコンバータMCP4922から出力される電圧によりフォトカプラのLEDの明るさを制御し、抵抗値を変えるという仕組みです。LFO周期の半分で増幅側のフォトカプラだけを動かし、もう半分で減衰側を動かすという動作となっています。電圧変化が直線的だと音量変化がスムーズでなかったため、独自にデータを取って電圧変化を指数カーブに修正しました。また、フォトカプラの特性はバラつきがあるので、増幅と減衰の幅が同じくらいになるようマッチングしました。<br>
<br>
AVRマイコンATmega328Pは内部クロック8MHzで動作させています。ポットやスイッチの読取（ADコンバータ）、LFO（DAコンバータ制御）、タップテンポといった機能を担っています。<br>
<br>
▽レイアウトについて（KiCadデータは<a href="https://github.com/kanengomibako/Tremolec">GitHub</a>へ）<br>
隙間が多いので表面実装でなくてもよかったかもしれません。一応アナログGNDとデジタルGNDを分けるようにしました。デジタルのノイズは測定限界以下で問題ないようです。C7、C9は何も入れておらず、後からコンデンサの値を調節できるように設けた部分になります。<br>
<br>
▽Arduinoスケッチ<br>
<code><pre>#include &lt;SPI.h&gt;<br>
<br>
// ピン設定 実際は高速処理するため該当レジスタを直接変更<br>
const byte swPin = 2;  // タップスイッチ<br>
const byte ledPin = 3; // PD3 周期表示用LED<br>
const byte SS1 = 10;   // PB2 MCP4922 Low<br>
const byte SS2 = 9;    // PB1 MCP4922 Mid<br>
const byte SS3 = 8;    // PB0 MCP4922 Treble<br>
const byte LDAC = 7;   // PD7 MCP4922 電圧出力作動ピン<br>
<br>
// LFO1周期を500分割 0.002周期経過するごとに電圧値を変更する<br>
volatile int pwmCount = 0;  // LFO用カウンタ  0〜499<br>
volatile int waveCount = 0; // 波形用カウンタ 0～125<br>
<br>
volatile unsigned long timeCount = 0; // タップ時間取得用カウンタ<br>
unsigned long tapTime = 0;            // タップ間隔時間 us<br>
unsigned long timeTemp = 0;           // タップ間隔時間一時保存用<br>
unsigned long swCount = 0;            // タップスイッチ用カウンタ<br>
<br>
byte adcNum = 0; // ADC番号（配列の添字）Bass0 Mid1 Tre2 Wave3 Div4 Rate5<br>
const byte adcPinArray[6] = {16, 17, 18, 19, 14, 15}; // ADCピン設定<br>
unsigned int oldAdcValue[6] = {2000, 2001, 2002, 2003, 2004, 2005}; // 旧ADC値<br>
unsigned int newAdcValue[6] = {1110, 1111, 1112, 1113, 1114, 1115}; // 新ADC値<br>
<br>
unsigned int dacArray[626]; // 電圧値配列 626段階 セットアップ時計算<br>
unsigned long dacDepth[3] = {0, 1, 2}; // 電圧値配列添字部分の倍率<br>
volatile unsigned int dacValueA[3] = {4001, 4002, 4003}; // 出力電圧値 増幅側<br>
volatile unsigned int dacValueB[3] = {4011, 4012, 4013}; // 出力電圧値 減衰側<br>
byte waveAmp = 1;   // 三角波→矩形波変換 増幅率<br>
const byte SQ = 12; // 上記増幅率設定値<br>
byte tapDiv = 1;    // タップ時間分割値<br>
//const float rateAdj = 1.000; // 周期補正 実測して設定→不要<br>
<br>
const unsigned int ledOnTime = 40000; // 周期表示LED点灯時間設定値 us<br>
unsigned int ledOffCount = 250;       // 上記LEDが消灯するカウンタ値<br>
<br>
void setup() {<br>
  TIMSK0 = 0; // Timer0割り込み停止 安定動作のため割り込みはTimer1のみ<br>
<br>
  // 電圧値の配列を計算 指数カーブ<br>
  for (int i = 0; i &lt;= 625; i++) {<br>
    dacArray[i] = 4141 - 46 * exp(0.0072 * i);<br>
  }<br>
<br>
  pinMode(swPin, INPUT_PULLUP);<br>
  pinMode(ledPin, OUTPUT);<br>
  pinMode(SS1, OUTPUT);<br>
  pinMode(SS2, OUTPUT);<br>
  pinMode(SS3, OUTPUT);<br>
  pinMode(LDAC, OUTPUT);<br>
<br>
  SPI.begin();<br>
  SPI.beginTransaction(SPISettings(8000000, MSBFIRST, SPI_MODE0)); // SPI 8MHz<br>
<br>
  // Timer1 電圧値変更間隔時間管理・タップ時間計測<br>
  TCCR1A = 0b00000000;     // Timer1 CTCモード<br>
  TCCR1B = 0b00001010;     // Timer1 CTCモード クロック8分周<br>
  OCR1A = 100;             // Timer1 比較Aの値 = 割り込み間隔 us<br>
  TIMSK1 |= (1 &lt;&lt; OCIE1A); // Timer1 比較A割り込み許可<br>
}<br>
<br>
// LFO（Timer1 比較A割り込み）-----------------------------------------------------<br>
ISR(TIMER1_COMPA_vect) {<br>
  timeCount++; // タップ時間取得用カウンタ<br>
  PORTD &amp;= ~_BV(7); // LDAC LOW (最初に電圧出力)<br>
  PORTD |= _BV(7);  // LDAC HIGH<br>
  if (pwmCount == 500) pwmCount = 0; // 0〜499 ループ<br>
  if (pwmCount &lt;= 125) {         // 三角波 増幅側上昇<br>
    waveCount = waveAmp * pwmCount;  // 三角波を増幅後、<br>
    waveCount = min(waveCount, 125); // クリップし矩形波とする<br>
    dacA(waveCount);<br>
  } else if (pwmCount &lt;= 250)  { // 三角波 増幅側下降<br>
    waveCount = waveAmp * (250 - pwmCount);<br>
    waveCount = min(waveCount, 125);<br>
    dacA(waveCount);<br>
  } else if  (pwmCount &lt;= 375) { // 三角波 減衰側上昇<br>
    waveCount = waveAmp * (pwmCount - 250);<br>
    waveCount = min(waveCount, 125);<br>
    dacB(waveCount);<br>
  } else {                       // 三角波 減衰側下降<br>
    waveCount = waveAmp * (500 - pwmCount);<br>
    waveCount = min(waveCount, 125);<br>
    dacB(waveCount);<br>
  }<br>
  if (pwmCount == 125) PORTD |= _BV(3); // 周期表示LED点灯<br>
  if (pwmCount == ledOffCount) PORTD &amp;= ~_BV(3); // 消灯<br>
  pwmCount++;<br>
}<br>
<br>
void loop() {<br>
  // タップテンポ -----------------------------------------------------------------<br>
  if (digitalRead(swPin) == LOW) {<br>
    swCount++;<br>
    if (swCount == 50) { // チャタリング対策 数msスイッチ押下で検出<br>
      // 前回スイッチ検出から経過した時間を記録<br>
      tapTime = timeTemp + timeCount * OCR1A  + TCNT1;<br>
      TCNT1 = 0; // 経過時間をリセット<br>
      timeTemp = 0;<br>
      timeCount = 0;<br>
      pwmCount = 124; // 増幅最大の点にリセット<br>
      if ( 200000 &lt; tapTime &amp;&amp; tapTime &lt; 2100000) { // 周期制限 0.2秒～2.1秒<br>
        // タップ間隔を電圧値変更間隔時間へ換算（Divスイッチ加味）<br>
        OCR1A = tapTime * 0.002 / tapDiv;<br>
        // どの周期でも周期表示LEDの点灯時間が同程度になるよう計算<br>
        ledOffCount = 125 + ledOnTime / OCR1A;<br>
      }<br>
    }<br>
  }<br>
  else swCount = 0;<br>
<br>
  // ADC --------------------------------------------------------------------------<br>
  adcNum++;<br>
  if (adcNum == 6) adcNum = 0; // 0〜5 ループ<br>
  newAdcValue[adcNum] = analogRead(adcPinArray[adcNum]); // 読取値が4以上変化で変更<br>
  if (abs(newAdcValue[adcNum] - oldAdcValue[adcNum]) &gt;= 4) {<br>
    if (adcNum &lt;= 2) { // Depth 0〜640  160までは急な変化<br>
      if (newAdcValue[adcNum] &lt; 100) dacDepth[adcNum] = 1.6 * newAdcValue[adcNum];<br>
      else dacDepth[adcNum] = 0.5201 * newAdcValue[adcNum] + 108;<br>
    } else if (adcNum == 3) { // Wave 1 or SQ 矩形波変換増幅率<br>
      if (newAdcValue[adcNum] &lt; 500) waveAmp = 1;<br>
      else waveAmp = SQ;<br>
    } else if (adcNum == 4) { // Div 1〜3 タップ時間分割値<br>
      tapDiv = 0.0029 * newAdcValue[adcNum] + 1;<br>
    } else if (adcNum == 5) { // Rate 4004〜133 電圧値変更間隔時間<br>
      // Rate変更前から経過した時間を記録<br>
      timeTemp = timeTemp + timeCount * OCR1A  + TCNT1;<br>
      TCNT1 = 0; // 経過時間をリセット<br>
      timeCount = 0;<br>
      // Rate計算 指数カーブ<br>
      OCR1A = 73 * exp(0.0039 * (1023 - newAdcValue[adcNum])) + 60;<br>
      // どの周期でも周期表示LEDの点灯時間が同程度になるよう計算<br>
      ledOffCount = 125 + ledOnTime / OCR1A;<br>
    }<br>
    oldAdcValue[adcNum] = newAdcValue[adcNum];<br>
  }<br>
}<br>
<br>
// 電圧値設定 A：増幅側 B：減衰側--------------------------------------------------<br>
void dacA(int w) { // w：waveCount 波形用カウンタ<br>
  // w(0〜125)にDepth(0〜640)をかけ128で割ったものが電圧値配列の添字(0〜625)<br>
  dacValueA[0] = dacArray[(dacDepth[0] * w) &gt;&gt; 7];<br>
  dacValueA[1] = dacArray[(dacDepth[1] * w) &gt;&gt; 7];<br>
  dacValueA[2] = dacArray[(dacDepth[2] * w) &gt;&gt; 7];<br>
  PORTB &amp;= ~_BV(2); // SS1(PB2) LOW<br>
  SPI.transfer((dacValueA[0] &gt;&gt; 8) | 0x30); // 0x30=0b00110000 A出力 バッファなし<br>
  SPI.transfer(dacValueA[0] &amp; 0xff);        // ゲイン1倍 シャットダウンなし<br>
  PORTB |= _BV(2);  // SS1(PB2) HIGH<br>
  PORTB &amp;= ~_BV(1); // SS2(PB1)<br>
  SPI.transfer((dacValueA[1] &gt;&gt; 8) | 0x30);<br>
  SPI.transfer(dacValueA[1] &amp; 0xff);<br>
  PORTB |= _BV(1);<br>
  PORTB &amp;= ~_BV(0); // SS3(PB0)<br>
  SPI.transfer((dacValueA[2] &gt;&gt; 8) | 0x30);<br>
  SPI.transfer(dacValueA[2] &amp; 0xff);<br>
  PORTB |= _BV(0);<br>
}<br>
<br>
void dacB(int w) {<br>
  dacValueB[0] = dacArray[(dacDepth[0] * w) &gt;&gt; 7];<br>
  dacValueB[1] = dacArray[(dacDepth[1] * w) &gt;&gt; 7];<br>
  dacValueB[2] = dacArray[(dacDepth[2] * w) &gt;&gt; 7];<br>
  PORTB &amp;= ~_BV(2);<br>
  SPI.transfer((dacValueB[0] &gt;&gt; 8) | 0xb0); // 0xb0=0b10110000 B出力<br>
  SPI.transfer(dacValueB[0] &amp; 0xff);<br>
  PORTB |= _BV(2);<br>
  PORTB &amp;= ~_BV(1);<br>
  SPI.transfer((dacValueB[1] &gt;&gt; 8) | 0xb0);<br>
  SPI.transfer(dacValueB[1] &amp; 0xff);<br>
  PORTB |= _BV(1);<br>
  PORTB &amp;= ~_BV(0);<br>
  SPI.transfer((dacValueB[2] &gt;&gt; 8) | 0xb0);<br>
  SPI.transfer(dacValueB[2] &amp; 0xff);<br>
  PORTB |= _BV(0);<br>
}</pre></code>過去記事（→<a href="00229.html">ATtiny85 タップテンポ付LFO その2</a>）と同じようなスケッチで、割り込み時の処理がDAコンバータ制御に変わっています。MCP4922の使い方は下記ページを参考にしました。高速処理するため、今回はレジスタを直接変更しています。<br>
<a href="https://zattouka.net/GarageHouse/micon/circuit/D_A.htm">きむ茶工房ガレージハウス - DAコンバータ　MCP4922(SPI)を利用しD/A変換を行う</a><br>
<br>
音については今までにない効果だと思うので、どう評価されるかわかりません。TREBLEのみ揺らす、MIDのみ揺らさない等、いろんなパターンを試せるので、誰でもきっと「お気に入りのトレモロ」が見つけられるんじゃないかと思います。<br>
<br>
<hr size="1"/><br>
以下、採用にならなかった案をメモしておきます。<br>
<br>
＜他のイコライザ回路＞<br>
下記ページのような3バンドイコライザ回路があります。DJミキサーに使われているらしいです。<br>
<a href="https://sound.whsites.net/articles/eq.htm#s9">Equalisers, The Various Types And How They Work - 9 - Frequency 'Isolators'</a><br>
シミュレーションしてみたのですが、減衰時の特性がいまいち気に入りませんでした。<br>
<br>
＜デジタルポテンショメータ（以下DPOT）＞<br>
フォトカプラの部分にDPOTを使うことを最初検討していました。しかし普通のDPOTは分解能が8ビットなので、1/256ずつ飛び飛びに値を動かすことになり、ノイズが発生してしまうことになります。常にDPOTを動かし続ける今回の用途には向いていないと判断しました。<br>
<br>
＜フォトカプラのPWM制御＞<br>
フォトカプラのLED側をPWMで制御しようと思い、PWM出力が6つ使えるマイコンATmega1284Pを準備しました。しかしPWMが一部8ビットなので、256段階でしか抵抗値を調節できず、スムーズに増幅・減衰の変化をさせることができませんでした。後から考えると、高性能な32ビットマイコンを使えばよかったかもしれません。<br>
<br>

</main>
<footer>
  <p><a href="../">ホーム</a></p>
  <p>Copyright &copy; 可燃ごみ箱</p>
</footer>
</body>
</html>